apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.getkeepsafe.dexcount'
apply from: "$project.rootDir/secretconfig.gradle"

android {

    compileSdkVersion COMPLIE_SDK_VERSION

    defaultConfig {
        applicationId "com.lounah.moneytracker"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode VERSION_CODE
        versionName VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        config {
            keyAlias signConfigs.keyAlias
            keyPassword signConfigs.keyPassword
            storeFile file(signConfigs.storeFile)
            storePassword signConfigs.storePassword
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "FIXER_API_KEY", "\"" + apiKeys.fixerIo + "\""
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    // modules
    implementation project(':core')
    // support
    implementation libraries.supportAppCompat
    implementation libraries.supportDesign
    implementation libraries.supportConstraintLayout
    implementation libraries.supportCardView
    // kotlin
    implementation libraries.kotlin
    // dagger
    implementation libraries.dagger
    implementation libraries.daggerAndroid
    kapt libraries.daggerAndroidProcessor
    kapt libraries.daggerCompiler
    // rx
    implementation libraries.rxKotlin
    implementation libraries.rxAndroid
    implementation libraries.rxBinding
    // jetpack architecture
    implementation libraries.archExtensions
    implementation libraries.reactivestreams
    kapt libraries.lifecycleProcessor
    implementation libraries.room
    implementation libraries.roomRx
    kapt libraries.roomProcessor
    // navigation
    implementation libraries.cicerone
    // network
    implementation libraries.retrofit
    implementation libraries.retrofitGsonConverter
    implementation libraries.retrofitRxJavaAdapter
    // developer utils
    debugImplementation libraries.leakCanary
    releaseImplementation libraries.leakCanaryNoOp
    implementation libraries.timber
    // temporary
    implementation 'com.ncapdevi:frag-nav:2.4.0'
    implementation 'org.fabiomsr:moneytextview:1.1.0'
    implementation 'com.github.aakira:expandable-layout:1.6.0@aar'
    implementation 'info.hoang8f:android-segmented:1.0.6'
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    // test
    testImplementation libraries.junit
    testImplementation libraries.hamcrest
    testImplementation libraries.mockito
    testImplementation libraries.roomTest
    testImplementation libraries.espresso
}
